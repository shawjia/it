#!/usr/bin/osascript

on play_some()
  tell application "iTunes"
    if player state is stopped then play some track of library playlist 1
  end tell
end play_some

on current_track_info()
  tell application "iTunes"
    return¬
      "\n----------------------------------------" &¬
      "\n|   name: " & name of current track  &¬
      "\n|  album: " & album of current track  &¬
      "\n| artist: " & artist of current track  &¬
      "\n----------------------------------------"
  end tell
end current_track_info


on run argv
  set help_txt to "Example usage:
Base:
    it play
    it pause
    it toggle
    it stop
Track:
    it next
    it previous(pre)
volume:
    it up
    it down
    it top
    it mute
Help:
    it help"


  if argv is {} then return help_txt

  set cmd to item 1 of argv

  if cmd is "play" then
    play_some()
    tell application "iTunes" to play
    return "Enjoy your music" & current_track_info()

  else if cmd is "pause" then
    tell application "iTunes" to pause
    return "Take a break"

  else if cmd is "toggle" then
    play_some()
    tell application "iTunes" to playpause
    return "To play or not to play, that's your choice" & current_track_info()

  else if cmd is "stop" then
    tell application "iTunes" to stop
    return "It's not the end"

  else if cmd is "next" then
    play_some()
    tell application "iTunes" to next track
    return "Give me something new" & current_track_info()

  else if cmd is "previous" or cmd is "pre" then
    play_some()
    tell application "iTunes" to previous track
    return "Love you all the time" & current_track_info()

  else if cmd is "up" then
    tell application "iTunes" to set sound volume to sound volume + 5
    return "Like your sound"

  else if cmd is "down" then
    tell application "iTunes" to set sound volume to sound volume - 5
    return "Quiet now"

  else if cmd is "top" then
    tell application "iTunes" to set sound volume to 100
    return "Give me your power"

  else if cmd is "mute" then
    --tell application "iTunes" to pause
    tell application "iTunes" to set sound volume to 0
    return "Silent tells all"

  else
    return help_txt

  end if

end run


-- vim: set ft=applescript ts=2 sw=2 et:
