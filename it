#!/usr/bin/env osascript

on play_some()
  tell application "iTunes"
    if player state is stopped then try
      play some track of playlist "Music"
    on error
      play some track of library playlist 1
    end try
  end tell
end play_some

on current_track_info()
  tell application "iTunes"
    return¬
      "\n+---------------------------------------" &¬
      "\n|   name: " & name of current track &¬
      "\n|  album: " & album of current track &¬
      "\n| artist: " & artist of current track &¬
      "\n| length: " & time of current track &¬
      "\n| played: " & played count of current track & " times" &¬
      "\n| rating: " & rating of current track &¬
      "\n+---------------------------------------"
  end tell
end current_track_info

on setting()
  tell application "iTunes"
    set now_pl to "none"
    set now_re to "none"
    set now_sh to "none"
    set now_vo to sound volume
    try
      set now_pl to name of current playlist
      set now_re to song repeat of current playlist
      if shuffle of current playlist is off then
        set now_sh to "off"
      else
        set now_sh to "on"
      end if
    end try
    return¬
      "+---------------------------------------\n" &¬
      "|   Repeat: " & now_re & "\n" &¬
      "|   Volume: " & now_vo & "\n" &¬
      "|  Shuffle: " & now_sh & "\n" &¬
      "| Playlist: " & now_pl & "\n" &¬
      "+---------------------------------------"

  end tell
end setting

on run argv
  set help_txt to "Example usage:
Base:
    it play/pause/toggle/stop
Track:
    it next(skip)/previous(pre)
Volume:
    it up/down/top/mute/volume(vol)
Rating:
    it rate 0~100
Repeat:
    it repeat off/one/all
Other:
    it uncheck(never)
    it shuffle off/on
    it setting(config)
Help:
    it help"


  if argv is {} then return help_txt

  set cmd to item 1 of argv

  if cmd is "play" then
    play_some()
    tell application "iTunes" to play
    return "Enjoy your music" & current_track_info()

  else if cmd is "pause" then
    tell application "iTunes" to pause
    return "Take a break"

  else if cmd is "toggle" then
    play_some()
    tell application "iTunes" to playpause
    return "To play or not to play, that's your choice" & current_track_info()

  else if cmd is "stop" then
    tell application "iTunes" to stop
    return "It's not the end"

  else if cmd is "next" or cmd is "skip" then
    play_some()
    tell application "iTunes" to next track
    return "Give me something new" & current_track_info()

  else if cmd is "previous" or cmd is "pre" then
    play_some()
    tell application "iTunes" to previous track
    return "Love you all the time" & current_track_info()

  else if cmd is "up" then
    tell application "iTunes"
      set sound volume to sound volume + 5
      return "Current sound volume is set to " & sound volume
    end tell

  else if cmd is "down" then
    tell application "iTunes"
      set sound volume to sound volume - 5
      return "Current sound volume is down to " & sound volume
    end tell

  else if cmd is "top" then
    tell application "iTunes" to set sound volume to 100
    return "Current sound volume is 100"

  else if cmd is "mute" then
    --tell application "iTunes" to pause
    tell application "iTunes" to set sound volume to 0
    return "Silent tells all"

  else if cmd is "volume" or cmd is "vol" then
    tell application "iTunes"
      return "Current sound volume is " & sound volume
    end tell

  else if cmd is "rate" then
    try
      set rate to item 2 of argv
      tell application "iTunes" to set rating of current track to rate
      return "Rating set to " & rate
    end try
    return "You should rate the song from 0 to 100"

  else if cmd is "repeat" then
    try
      set mode to item 2 of argv
      if mode is "off" then
        tell application "iTunes" to set song repeat of current playlist to off
      else if mode is "one" then
        tell application "iTunes" to set song repeat of current playlist to one
      else if mode is "all" then
        tell application "iTunes" to set song repeat of current playlist to all
      else
        return "Repeat mode should be off/one/all"
      end if
      return "Repeat mode set to " & mode
    end try
    return "Repeat mode should be off/one/all"

  else if cmd is "uncheck" or cmd is "never" then
    tell application "iTunes"
      set enabled of current track to false
      next track
    end tell
    return "You would never hear it again, enjoy the new one. " & current_track_info()

  else if cmd is "shuffle" then
    try
      set mode to item 2 of argv
      if mode is "off" then
        tell application "iTunes" to set shuffle of current playlist to false
      else if mode is "on" then
        tell application "iTunes" to set shuffle of current playlist to true
      else
        return "Shuffle mode should be off/on"
      end if
      return "Shuffle mode set to " & mode
    end try
    return "Shuffle mode should be off/on"

  else if cmd is "setting" or cmd is "config" then
    return setting()

  else
    return help_txt

  end if

end run


-- vim: set ft=applescript ts=2 sw=2 et:
